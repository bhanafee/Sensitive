plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'maven-publish'
}

group = 'com.maybeitssquid'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(toolchainJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

compileJava {
    options.release = targetJavaVersion as Integer
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation "org.mockito:mockito-core:$mockitoVersion"
                implementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = "sensitive"
            version = "$version"
            artifact jar
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/bhanafee/Sensitive"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}